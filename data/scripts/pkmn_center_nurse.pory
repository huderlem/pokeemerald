script Common_EventScript_PkmnCenterNurse {
    lock
    faceplayer
    setvar(VAR_0x8004, 0)
    specialvar(VAR_RESULT, CountPlayerTrainerStars)
    if (var(VAR_RESULT) == 4) {
        if (flag(FLAG_NURSE_MENTIONS_GOLD_CARD)) {
            msgbox(gText_YouWantTheUsual, MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                setvar(VAR_0x8004, 1)
                goto(EventScript_PkmnCenterNurse_HealPkmn)
            } else {
                message(gText_WeHopeToSeeYouAgain2)
            }
        } else {
            setflag(FLAG_NURSE_MENTIONS_GOLD_CARD)
            msgbox(gText_WelcomeCutShort)
            playse(SE_PIN)
            applymovement(VAR_0x800B, Common_Movement_ExclamationMark)
            waitmovement(0)
            applymovement(VAR_0x800B, Common_Movement_Delay48)
            waitmovement(0)
            msgbox(gText_NoticesGoldCard, MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                setvar(VAR_0x8004, 1)
                goto(EventScript_PkmnCenterNurse_HealPkmn)
            } else {
                message(gText_WeHopeToSeeYouAgain2)
            }
        }
    } else {
        msgbox(gText_WouldYouLikeToRestYourPkmn, MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            goto(EventScript_PkmnCenterNurse_HealPkmn)
        } else {
            message(gText_WeHopeToSeeYouAgain)
        }
    }
}

// VAR_0x8004 is 1 when player has Gold Card
script EventScript_PkmnCenterNurse_HealPkmn {
    incrementgamestat(GAME_STAT_USED_POKECENTER)
    if (var(VAR_0x8004) == 1) {
        message(gText_IllTakeYourPkmn2)
    } else {
        message(gText_IllTakeYourPkmn)
    }
    waitmessage
    applymovement(VAR_0x800B, Common_Movement_WalkInPlaceFastestLeft)
    waitmovement(0)
    dofieldeffect(FLDEFF_POKECENTER_HEAL)
    waitfieldeffect(FLDEFF_POKECENTER_HEAL)
    applymovement(VAR_0x800B, Common_Movement_WalkInPlaceFastestDown)
    waitmovement(0)
    special(HealPlayerParty)
    if (!flag(FLAG_POKERUS_EXPLAINED)) {
        specialvar(VAR_RESULT, IsPokerusInParty)
        if (var(VAR_RESULT) == TRUE) {
            message(gText_PokerusExplanation)
            setflag(FLAG_POKERUS_EXPLAINED)
            return
        }
    }
    specialvar(VAR_RESULT, PlayerNotAtTrainerHillEntrance)
    if (var(VAR_RESULT) == 0) {
        goto(EventScript_PkmnCenterNurse_ReturnPkmn)
    }
    specialvar(VAR_RESULT, BufferUnionRoomPlayerName)
    copyvar(VAR_0x8008, VAR_RESULT)
    if (var(VAR_0x8008) == 0 || flag(FLAG_NURSE_UNION_ROOM_REMINDER)) {
        goto(EventScript_PkmnCenterNurse_ReturnPkmn)
    }
    msgbox(gText_RestoredPkmnToFullHealth)
    setflag(FLAG_NURSE_UNION_ROOM_REMINDER)
    message(CableClub_Text_PlayerIsWaiting)
    waitmessage
    applymovement(VAR_0x800B, Movement_PkmnCenterNurse_Bow)
    waitmovement(0)
    message(gText_WeHopeToSeeYouAgain)
}

// VAR_0x8004 is 1 when player has Gold Card
script EventScript_PkmnCenterNurse_ReturnPkmn {
    if (var(VAR_0x8004) == 1) {
        message(gText_ThankYouForWaiting)
        waitmessage
        applymovement(VAR_0x800B, Movement_PkmnCenterNurse_Bow)
        waitmovement(0)
        message(gText_WeHopeToSeeYouAgain2)
    } else {
        message(gText_RestoredPkmnToFullHealth)
        waitmessage
        applymovement(VAR_0x800B, Movement_PkmnCenterNurse_Bow)
        waitmovement(0)
        message(gText_WeHopeToSeeYouAgain)
    }
}

movement Movement_PkmnCenterNurse_Bow {
    nurse_joy_bow
    delay_4
}
